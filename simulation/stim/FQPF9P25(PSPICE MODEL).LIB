*$
**************** Power Discrete MOSFET Electrical Circuit Model *****************
** Product Name: FQPF9P25
** P-Channel QFET 250V, -6A, 620mohm and TO-220F
** Model Type: BSIM3V3
**-------------------------------------------------------------------------------
.SUBCKT FQPF9P25 2 1 3
*Nom Temp=25 deg C
Dbody 5 7 DbodyMOD
Dbreak 7 11 DbreakMOD
Lgate 1 9 1.12e-9
Ldrain 2 5 0.5e-9
Lsource 3 7 9.10e-10
RLgate 1 9 11.2
RLdrain 2 5 5.0
RLsource 3 7 9.1
Rgate 9 6 0.5
It 7 17 1
Ebreak 5 11 17 7 -275
Rbreak 17 7 RbreakMOD 1
.MODEL RbreakMOD RES (TC1=8.8e-4 TC2=-1.09e-6)
.MODEL DbodyMOD D (IS=6.5e-12  N=1.5  RS=0.0852   TRS1=1.02e-5  TRS2=5.1e-7
+ CJO=1.38e-9    M=0.65    VJ=0.63    TT=3.13e-7  XTI=18        EG=1.18)
.MODEL DbreakMOD D (RS=0.1 TRS1=1e-3 TRS2=1e-6)
Rdrain 5 16 RdrainMOD 0.456
.MODEL RdrainMOD RES (TC1=7.7e-3 TC2=1.20e-5)
M_BSIM3 16 6 7 7 Bsim3 W=2.314 L=2.0e-6 NRS=1
.MODEL Bsim3 PMOS (LEVEL=7 VERSION=3.1 MOBMOD=3 CAPMOD=2 PARAMCHK=1 NQSMOD=0
+ TOX=970e-10      XJ=1.4e-6       NCH=1.3e17
+ U0=220           VSAT=1e5        DROUT=1.0
+ DELTA=0.1        PSCBE2=0        RSH=6.5e-3
+ VTH0=-4.05       VOFF=-0.1       NFACTOR=1.1
+ LINT=4.17e-7     DLC=4.17e-7     CGSO=1.1e-15
+ CGSL=1.0e-15     CGDO=1.06e-14   CGDL=6.25e-10
+ CJ=0             CF=0            CKAPPA=0.1
+ KT1=-1.35        KT2=0           UA1=-2.61e-9
+ NJ=10)
.ENDS 
*$
******************* Power Discrete MOSFET Thermal Model *************************
** Package: TO-220F
**-------------------------------------------------------------------------------
.SUBCKT FQPF9P25_THERMAL TH TL
CTHERM1 TH 6 2.1e-4
CTHERM2 6 5  7.1e-3
CTHERM3 5 4  5.8e-2
CTHERM4 4 3  9.4e-2
CTHERM5 3 2  4.6e-1
CTHERM6 2 TL 1.2e-0 
RTHERM1 TH 6 9.20e-3
RTHERM2 6 5  6.10e-2
RTHERM3 5 4  1.20e-1
RTHERM4 4 3  3.60e-1
RTHERM5 3 2  7.30e-1
RTHERM6 2 TL 1.22e+0
.ENDS FQPF9P25_THERMAL
**-------------------------------------------------------------------------------
** Creation: Aug.-18-2017   Rev: 1.0
** ON Semiconductor
*$

